plugins {
    id 'java'
    id 'maven'
    id 'maven-publish'
    id 'jacoco'
    id "com.jfrog.bintray" version "1.2"
}

sourceCompatibility = 1.8
version = '0.8.4'
group = 'org.mrcsparker'

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        mavenJava(MavenPublication) {  // Artifacts to publish
            from components.java
            artifact sourceJar
            artifact javadocJar
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')

    dryRun = false
    publish = true

    publications = ['mavenJava']

    pkg {
        repo = 'maven'         // The repository
        name = 'ceeql'         // The package
        desc = 'Utilities to generate a standard toString of a class using Jdk8 features.'
        websiteUrl = 'https://github.com/mrcsparker/ceeql'
        issueTrackerUrl = 'https://github.com/mrcsparker/ceeql/issues'
        vcsUrl = 'https://github.com/mrcsparker/ceeql.git'
        licenses = ['LGPL-3.0']
        labels = ['SQL', 'JSON']
        publicDownloadNumbers = true
    }
}

jacoco {
    toolVersion = "0.7.1.201405082137"
    reportsDir = file("$buildDir/customJacocoReportDir")
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.destination "${buildDir}/jacocoHtml"
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'org.jdbi', name: 'jdbi', version: '2.63.1'
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.3'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.6.1'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.6.1'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.6.1'
    compile group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-csv', version: '2.6.1'
    compile group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-xml', version: '2.6.1'
    compile group: 'com.zaxxer', name: 'HikariCP', version: '2.3.9'
    compile group: 'com.github.jknack', name: 'handlebars', version: '2.2.3'
    compile group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.3'
    compile group: 'org.apache.logging.log4j', name: 'log4j-1.2-api', version: '2.3'
    compile group: 'com.opencsv', name: 'opencsv', version: '3.4'
    compile group: 'com.codahale.metrics', name: 'metrics-core', version: '3.0.2'
    testCompile group: 'com.h2database', name: 'h2', version: '1.4.188'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

test {
    testLogging {
        // Show that tests are run in the command-line output
        events 'started', 'passed'
    }
    jacoco {
        append = false
        destinationFile = file("$buildDir/jacoco/jacocoTest.exec")
        classDumpFile = file("$buildDir/jacoco/classpathdumps")
    }
}

